# Analyze a Tic-Tac-Toe Board

###### Hard
###### #game

A [tic-tac-toe](http://en.wikipedia.org/wiki/Tic-tac-toe) board is represented by a two dimensional vector. X is represented by :x, O is represented by :o, and empty is represented by :e. A player wins by placing three Xs or three Os in a horizontal, vertical, or diagonal row. Write a function which analyzes a tic-tac-toe board and returns :x if X has won, :o if O has won, and nil if neither player has won.

# Test Cases
```clojure
(= nil (__ [[:e :e :e]
            [:e :e :e]
            [:e :e :e]]))
```
```clojure
(= :x (__ [[:x :e :o]
           [:x :e :e]
           [:x :e :o]]))
```
```clojure
(= :o (__ [[:e :x :e]
           [:o :o :o]
           [:x :e :x]]))
```
```clojure
(= nil (__ [[:x :e :o]
            [:x :x :e]
            [:o :x :o]]))
```
```clojure
(= :x (__ [[:x :e :e]
           [:o :x :e]
           [:o :e :x]]))
```
```clojure
(= :o (__ [[:x :e :o]
           [:x :o :e]
           [:o :e :x]]))
```
```clojure
(= nil (__ [[:x :o :x]
            [:x :o :x]
            [:o :x :o]]))
```

# Solution
```clojure
(fn xo [board]
  (let [diagonal1 (map #(get-in board [%1 %2]) (range 3) (range 3))
        diagonal2 (map #(get-in board [%1 %2]) (reverse (range 3)) (range 3))
        diagonals (vector diagonal1 diagonal2)
        rows board
        columns (apply mapv vector board)]
        ((comp first first) (filter #(and (apply = %) (not= (first %) :e)) (concat diagonals rows columns)))))
```
