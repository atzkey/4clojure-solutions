# Balancing Brackets

###### Medium
###### #parsing

When parsing a snippet of code it's often a good idea to do a sanity check to see if all the brackets match up. Write a function that takes in a string and returns truthy if all square [] round ( ) and curly { } brackets are properly paired and legally nested, or returns falsey otherwise.

# Test Cases
```clojure
(__ "This string has no brackets.")
```
```clojure
(__ "class Test {
      public static void main(String[] args) {
        System.out.println(\"Hello world.\");
      }
    }")
```
```clojure
(not (__ "(start, end]"))
```
```clojure
(not (__ "())"))
```
```clojure
(not (__ "[ { ] } "))
```
```clojure
(__ "([]([(()){()}(()(()))(([[]]({}()))())]((((()()))))))")
```
```clojure
(not (__ "([]([(()){()}(()(()))(([[]]({}([)))())]((((()()))))))"))
```
```clojure
(not (__ "["))
```

# Solution
```clojure
(fn [string]
  (let [
    brackets {\( \), \{ \}, \[ \]}
    brackets' (clojure.set/map-invert brackets)
    opening-brackets (set (vals brackets'))
    closing-brackets (set (keys brackets'))
    balanced? (fn [[x & xs] stack]
      (cond
        (nil? x) (empty? stack)
        (contains? opening-brackets x) (recur xs (conj stack x))
        (contains? closing-brackets x)
          (and
            (= (peek stack) (get brackets' x))
            (recur xs (pop stack)))
        :else (recur xs stack))
        
      )
    ]
    (balanced? string '())))
```
